  10 DEF FN Z(J)=INT (J/6)*4+3: DEF FN X(J)=(J-INT (J/6)*6)*5+1
  20 INK 6: PAPER 0: OVER 0: FLASH 0: BORDER 0: BRIGHT 1: INVERSE 0: CLS : GO TO 60
  30 CLEAR 47418
  40 PAPER 0: INK 0: BORDER 0: BRIGHT 1: FLASH 0: INVERSE 0: OVER 0: CLS
  50 LOAD "CODE"CODE : INK 6: POKE 23606,88: POKE 23607,244
  60 POKE 23658,8: DIM V(40,2): DIM I(13,2): DIM U(5): RESTORE 2480: FOR H=1 TO 40: READ V(H,1),V(H,2): NEXT H: RESTORE 2040: FOR H=1 TO 13: READ I(H,1),I(H,2): NEXT H
  70 DIM R(48): RESTORE 2030: FOR H=1 TO 48: READ R(H): NEXT H
  80 DIM P(133): FOR H=1 TO 133: LET P(H)=PEEK (H+47418): IF P(H)>250 THEN LET P(H)=P(H)-256
  90 NEXT H
 100 LET X2=0: LET Y2=0: LET LEV=0: LET L=0
 110 LET U$="": FOR J=47576 TO 47580: LET U$=U$+CHR$ PEEK J: NEXT J
 120 PRINT AT 1,4; PAPER 5; INK 0;" B U M P Y - THE EDITOR "
 130 PRINT AT 18,1; INK 4;"""";U$(3);"""... UP"," """;U$(4);"""... DOWN"," """;U$(1);"""... LEFT"," """;U$(2);"""... RIGHT"," """;U$(5);"""... FIRE"
 140 PLOT 0,0: DRAW 255,0: DRAW 0,39: DRAW -255,0: DRAW 0,-39: PLOT 2,2: DRAW 251,0: DRAW 0,35: DRAW -251,0: DRAW 0,-35
 150 PRINT AT 6,10;"LOAD DATA"''"          SAVE DATA"''"          EDIT"''"          INSTRUCTIONS"''"          DEFINE KEYS"
 160 LET IU=6: LET OIU=5
 170 PRINT AT IU,5; OVER 1; PAPER 2;"                      ";AT OIU,5; OVER 1; PAPER 0;"                      "
 180 LET N$=INKEY$
 190 IF N$<>U$(3) AND N$<>U$(4) AND N$<>U$(5) THEN GO TO 180
 200 LET OIU=IU
 210 IF N$=U$(4) THEN LET IU=IU+2: IF IU=16 THEN LET IU=6
 220 IF N$=U$(3) THEN LET IU=IU-2: IF IU=4 THEN LET IU=14
 230 IF N$=U$(5) THEN GO TO 250
 240 GO TO 170
 250 LET OT=IU/2-2
 260 IF OT=1 THEN CLS : PRINT AT 5,2;"(PRESSING RETURN BEFORE TYPINGIN THE FILENAME WILL TELL THE   COMPUTER TO LOAD WHAT EVER COMESNEXT.)": GO SUB 2270: CLS : INK 5: LOAD F$CODE 55050: CLS : INK 6: GO TO 120
 270 IF OT=2 THEN CLS : GO SUB 2270: IF F$="" THEN PRINT AT 7,5; FLASH 1;" INVALID FILENAME! ": PAUSE 0: GO TO 270
 280 IF OT=2 THEN GO SUB 2370: SAVE F$CODE 55050,L*75+75
 290 IF OT=2 THEN CLS : PRINT AT 7,7; PAPER 4; BRIGHT 1; INK 0;" REWIND TAPE ";AT 9,8;"(VERIFYING)": VERIFY F$CODE : CLS : GO TO 120
 300 IF OT=3 THEN CLS : GO SUB 360: GO TO 300
 310 IF OT=4 THEN GO SUB 590: GO TO 110
 320 IF OT=5 THEN GO SUB 1170: GO TO 110
 330 PRINT AT KH,KJ+1;"LEFT: "; PAPER 6; INK 0;" """;U$(1);""" "; PAPER 0; INK 6;AT KH+2,KJ;"RIGHT: "; PAPER 6; INK 0;" """;U$(2);""" "
 340 PRINT AT KH+4,KJ+3;"UP: "; PAPER 6; INK 0;" """;U$(3);""" "; PAPER 0; INK 6;AT KH+6,KJ+1;"DOWN: "; PAPER 6; INK 0;" """;U$(4);""" "
 350 PRINT AT KH+9,KJ+1; INK 6;"FIRE: "; PAPER 6; INK 0;" """;U$(5);""" ": RETURN
 360 GO SUB 400
 370 GO SUB 1420
 380 IF H=59 THEN RETURN
 390 IF H=0 THEN IF M$="6" THEN GO TO 370
 400 IF INKEY$=U$(5) THEN GO TO 400
 410 LET X$=" 00"( TO 3-LEN STR$ LEV)+STR$ LEV+" ": LET KH=11: LET KJ=4: GO SUB 330: PRINT AT 11,20; INK 5;"= -1";AT 13,20;"= +1";AT 15,20;"= +5";AT 17,20;"= -5";AT 20,20;"= SELECT";AT 5,4; INK 4;"LEVEL (0 TO 99): "; PAPER 6; INK 0;X$: OVER 0
 420 PLOT 0,0: DRAW 255,0: DRAW 0,95: DRAW -255,0: DRAW 0,-95: PLOT 2,2: DRAW 251,0: DRAW 0,91: DRAW -251,0: DRAW 0,-91
 430 LET N$=INKEY$: IF N$<>U$(3) AND N$<>U$(4) AND N$<>U$(5) AND N$<>U$(1) AND N$<>U$(2) THEN GO TO 430
 440 IF N$=U$(4) THEN LET LEV=LEV-5: IF LEV<0 THEN LET LEV=LEV+100
 450 IF N$=U$(3) THEN LET LEV=LEV+5: IF LEV>99 THEN LET LEV=LEV-100
 460 IF N$=U$(2) THEN LET LEV=LEV+1: IF LEV=100 THEN LET LEV=0
 470 IF N$=U$(1) THEN LET LEV=LEV-1: IF NOT LEV+1 THEN LET LEV=99
 480 IF N$=U$(5) THEN GO TO 510
 490 LET Z$=" 00"( TO 3-LEN STR$ LEV)+STR$ LEV+" ": PRINT AT 5,21; PAPER 6; INK 0;Z$
 500 GO TO 430
 510 CLS
 520 RESTORE 550: FOR k=0 TO 23: LET O=PEEK (47552+K): GO SUB 580
 530 IF k<22 THEN PRINT AT k,0; BRIGHT ab; INK ai; PAPER 0;"#": LET o=o+3: GO SUB 580: PRINT AT k,30; BRIGHT ab; PAPER 0; INK ai;"#": NEXT k
 540 PRINT #0;AT k-22,0; BRIGHT ab; INK ai; PAPER 0;"#": LET o=o+3: GO SUB 580: PRINT #0;AT k-22,30; BRIGHT ab; INK ai; PAPER 0;"#": NEXT k
 550 GO SUB 1140: FOR G=0 TO 35: IF INKEY$="6" THEN GO SUB 1380: IF V$="Y" THEN LET M$="6": GO SUB 910: RETURN
 560 LET B1=INT (PEEK (LEV*75+55050+G)/16): LET B2=PEEK (LEV*75+55050+G)-B1*16
 570 LET BB=PEEK (LEV*75+55089+G): GO SUB 980: GO SUB 1010: GO SUB 1070: NEXT G: GO SUB 1030: GO SUB 1050: RETURN
 580 POKE 62800,o*8+62808-INT ((o*8+62808)/256)*256: POKE 62801,INT ((o*8+62808)/256): LET ai=PEEK (o+62455)-INT (PEEK (o+62455)/8)*8: LET ab=(ai<>PEEK (o+62455)): RANDOMIZE USR 62796: RETURN
 590 CLS : RESTORE 640: FOR J=1 TO 3: FOR H=1 TO 3: READ E$: PRINT AT H*6-6,3; INK 6*(H=1)+5*(H=2)+4*(H=3);E$
 600 IF INKEY$<>"" THEN GO TO 600
 610 IF INKEY$="" THEN GO TO 610
 620 IF INKEY$<>"" THEN GO TO 620
 630 CLS : NEXT H: NEXT J
 640 DATA "WHEN YOU CHOOSE TO EDIT, YOU WILL BE ASKED TO SELECT A LEVEL FOR EDITING. HAVING DONE THIS,  THE LEVEL WILL BE DISPLAYED.    WHILE IT IS DOING THIS, YOU CAN PRESS '6' TO CLEAR THE LEVEL. IFYOU DO, YOU WILL THEN BE ASKED  TO CONFIRM THAT YOU WISH IT TO  BE WIPED. WHEN DISPLAYED, AT THETOP OF THIS LEVEL ARE FIVE UDGS.OF THOSE:"
 650 DATA "                             THE DRIP (|) REPRESENTS HOW MANY             OBJECTS YOU NEED TO             COLLECT BEFORE                  MOVING TO THE NEXT              LEVEL. (THIS DOES               NOT INCLUDE 1UPS,               HAMMERS, DRIPS OR               KEYS.)"
 660 DATA "THE LIGHTNING BOLT ("+CHR$ 16+CHR$ 2+"©"+CHR$ 16+CHR$ 4+") TELLS YOU WHICH LEVEL YOU ARE EDITING"
 670 DATA "TO EDIT THE LEVEL, USE YOUR  DEFINED KEYS TO SELECT SOMEWHERETO PUT/DELETE/EXCHANGE AN       OBJECT/PLATFORM/WALL. WHEN YOU  HAVE SELECTED, YOU WILL BE      SHOWN A CHOICE OF ALL THE       DIFFERENT OBJECTS/PLATFORMS/    WALLS. YOU CAN THEN SELECT FROM THESE THE ONE THAT YOU WISH TO  PUT THERE."
 680 DATA "ON THIS SCREEN:                                              BUMPY REPRESENTS THE STARTING   POSITION                        THE MAGENTA FINISHING SPRITE    DOES NOT REPRESENT THE FINISHINGPOSITION, BUT IS AN INVISIBLE   'CHEAT' WHICH WHEN LANDED UPON  WILL TAKE YOU TO THE NEXT LEVEL."
 690 DATA "MELTING PLATFORMS ARE "+CHR$ 16+CHR$ 1+"BLUE"+CHR$ 16+CHR$ 4+";  STURDY PLATFORMS ARE "+CHR$ 16+CHR$ 7+"WHITE"+CHR$ 16+CHR$ 4+".                                        SELECTING ANY EMPTY SPACE    WILL GIVE YOU A FURTHER SIX     OPTIONS WHICH WILL BE SCROLLED  ACROSS THE TOP OF THE OTHER     SCREEN. ALTERNATIVELY, YOU CAN  SIMPLY PRESS THE APPROPRIATE    NUMBER WHILE EDITING."
 700 DATA "TO DUMP A LEVEL TO THE       PRINTER, PRESS THE EDIT KEY ON  YOUR COMPUTER WHILST ON THE     APPROPRIATE SCREEN ('SHIFT' AND '1' IF YOU HAVE NO EDIT KEY).   SAM USERS ARE LIKELY TO FIND    THEIR SAM WILL 'FREEZE UP' ON   THEM IF THEY TRY."
 710 DATA "ALSO NOTE SAM USERS THAT IF  YOU LOAD THIS PROGRAM THROUGH   THE PROVIDED BASIC TRANSLATOR,  YOU WILL PROBABLY FIND THAT WHENYOU GO TO EDIT, YOUR SAM WILL   CRASH. INSTEAD, LOAD THIS       PROGRAM THROUGH SOME SORT OF    EMULATOR, E.G. SAMTAPE."
 720 DATA "I'D BETTER BE SIGNING OFF NOWAS AN EXHAUSTED 14 YEAR OLD FROMA TOTALY DESOLATED PART OF      BRITAIN CALLED ULLINGTON (SORT  OF EVESHAM/STRATFORD WAY) WHO'S DYING OF HUNGER, AND LIKES      NOTHING BETTER THAN HIS DINNER!"
 730 PRINT AT 4,3;"HAVE FUN, ";AT 10,14;"PONDOROUS";AT 13,10; INK 2;"(SOMETIMES REFERED";AT 14,10;"TO AS PETER MOORE)"
 740 PAUSE 0: CLS : RETURN
 750 OVER 0: LET W$="       1: EXIT OPTIONS              2: CLEAR WALL                3: CLEAR PLATFORM            4: CLEAR OBJECT              "
 760 LET W$=W$+"5: CLEAR ALL 3               6: CLEAR SCREEN                                                            "
 770 FOR G=1 TO LEN W$-58 STEP 29
 780 PRINT AT 0,1;W$(G TO G+28)
 790 LET M$=INKEY$: IF M$="" THEN GO TO 790
 800 IF M$="6" THEN GO SUB 1380: IF V$="N" THEN RETURN
 810 IF M$>"0" AND M$<"7" THEN GO TO 840
 820 FOR J=0 TO 28: PRINT AT 0,1;W$(G+J TO G+J+28): NEXT J
 830 NEXT G: GO TO 770
 840 OVER 1
 850 IF M$="1" THEN GO SUB 1140: RETURN
 860 LET G=POS: IF M$="2" OR M$="3" OR M$="5" THEN LET B1=INT (PEEK (LEV*75+55050+POS)/16): LET B2=PEEK (LEV*75+55050+POS)-B1*16
 870 IF M$="2" OR M$="5" THEN POKE LEV*75+55050+POS,B2: GO SUB 1010: LET B1=0
 880 IF M$="3" OR M$="5" THEN GO SUB 980: POKE LEV*75+55050+POS,B1*16: IF B2=11 THEN FOR J=FN X(POS)+32*FN Z(POS)+22528 TO FN X(POS)+32*FN Z(POS)+22531: POKE J,70+128*INT (PEEK J/128): NEXT J
 890 IF M$="4" OR M$="5" THEN LET BB=PEEK (LEV*75+55089+POS): GO SUB 1070: POKE LEV*75+55089+POS,0
 900 IF M$<"6" THEN GO SUB 1140: RETURN
 910 IF M$="6" THEN FOR J=LEV*75+55050 TO LEV*75+55124: POKE J,0: NEXT J: POKE LEV*75+55087,1: CLS : GO SUB 510: RETURN
 920 STOP
 930 LET O=N: GO SUB 580: LET Y1=Y: LET X1=X: GO SUB 940: FOR S=P(T) TO P(T+1)-1 STEP 2: LET O=(S-P(T))/2+N+1: GO SUB 580: LET Y1=Y+P(S): LET X1=X+P(S+1): GO SUB 940: NEXT S: RETURN
 940 LET PP=0: IF O>190 AND O<195 THEN LET PP=3
 950 IF Y1>21 AND Y1<24 THEN PRINT #0;AT Y1-22,X1; PAPER PP; INK AI; BRIGHT AB; OVER 1; FLASH 8;"#": RETURN
 960 IF Y1<22 AND Y1>0 THEN PRINT AT Y1,X1; PAPER PP; INK AI; BRIGHT AB; OVER 1; FLASH 8;"#": RETURN
 970 RETURN
 980 IF B2=0 THEN RETURN
 990 IF B2=2 THEN LET N=96: LET Y=FN Z(G): LET X=FN X(G): FOR S=62551 TO 62554: POKE S,9: NEXT S: LET T=2: GO SUB 930: FOR S=62551 TO 62554: POKE S,7: NEXT S: RETURN
1000 LET N=R(B2*4-3): LET Y=FN Z(G)+R(B2*4-2): LET X=FN X(G)+R(B2*4-1): LET T=R(B2*4): GO SUB 930: RETURN
1010 IF B1=0 THEN RETURN
1020 LET N=B1*3+159: LET Y=FN Z(G)-3: LET X=FN X(G)+4: LET T=3: GO SUB 930: RETURN
1030 LET N=174: LET F=PEEK (LEV*75+55086): LET Y=FN Z(F)-3: LET X=FN X(F)+1: LET T=12: GO SUB 930
1040 RETURN
1050 LET N=PEEK (LEV*75+55088): LET Y=FN Z(N): LET X=FN X(N): FOR R=22528+Y*32+X TO 22531+Y*32+X: POKE R,PEEK R+128: NEXT R
1060 RETURN
1070 IF BB=0 THEN RETURN
1080 LET N=V(BB,1): LET Y=FN Z(G)+I(V(BB,2),1): LET X=FN X(G)+I(V(BB,2),2)
1090 LET T=V(BB,2): GO SUB 930: RETURN
1100 OVER 0: PRINT AT 0,1;"     DO YOU WISH TO DUMP?    "
1110 IF INKEY$="Y" THEN GO SUB 1140: COPY : OVER 1: RETURN
1120 IF INKEY$="N" THEN GO SUB 1140: OVER 1: RETURN
1130 GO TO 1110
1140 OVER 0: LET L$="00"( TO 2-LEN STR$ LEV)+STR$ LEV: LET Q$="00"( TO 2-LEN STR$ PEEK (55087+LEV*75))+STR$ PEEK (55087+LEV*75)
1150 PRINT AT 0,1;"  "; INK 7; BRIGHT 0;"00000 "; INK 6; BRIGHT 1;"{"; INK 7; BRIGHT 0;"00"; INK 5; BRIGHT 1;"  |"; INK 7; BRIGHT 0;Q$; INK 5; BRIGHT 1;" }"; INK 7; BRIGHT 0;"00 "; INK 6; BRIGHT 1;"~"; INK 7; BRIGHT 0;"00 "; INK 2; BRIGHT 1;"©"; INK 7; BRIGHT 0;L$;" "
1160 OVER 1: RETURN
1170 CLS : LET BCV=1: LET KH=4: LET KJ=7: GO SUB 330
1180 FOR J=1 TO 5: LET U(J)=CODE U$(J): FOR H=22597+64*J+32*(J=5) TO 22612+64*J+32*(J=5): POKE H,104: NEXT H: IF J>1 THEN FOR H=22533+64*J TO 22548+64*J: POKE H,7: NEXT H
1190 LET N$=INKEY$: IF NOT ((N$>="A" AND N$<="Z") OR N$=" ") THEN GO TO 1190
1200 IF J>1 THEN FOR W=1 TO J-1: IF N$<>CHR$ U(W) THEN NEXT W: GO TO 1220
1210 IF J>1 THEN GO TO 1190
1220 LET Z1=U(J)-32: IF Z1>0 THEN LET Z1=Z1-32
1230 LET Z2=CODE N$-32: IF Z2>0 THEN LET Z2=Z2-32
1240 IF Z1=Z2 THEN FOR K=Z1 TO Z1
1250 IF Z2>Z1 THEN IF Z1+27-Z2>Z2-Z1 THEN FOR K=Z1 TO Z2
1260 IF Z2>Z1 THEN IF Z1+27-Z2<Z2-Z1 THEN FOR K=Z1+27 TO Z2 STEP -1
1270 IF Z2<Z1 THEN IF Z2+27-Z1>Z1-Z2 THEN FOR K=Z1 TO Z2 STEP -1
1280 IF Z2<Z1 THEN IF Z2+27-Z1<Z1-Z2 THEN FOR K=Z1 TO Z2+27
1290 LET I=K+32: IF I>32 THEN LET I=I+32
1300 IF I>90 THEN LET I=I-59
1310 IF I>32 AND I<65 THEN LET I=I+32
1320 IF I<32 THEN LET I=I+59
1330 IF I<65 AND I>32 THEN LET I=I-32
1340 IF J<5 THEN PRINT AT J*2+2,16; PAPER 5; INK 0;CHR$ I: NEXT K
1350 IF J=5 THEN PRINT AT 13,16; PAPER 5; INK 0;CHR$ I: NEXT K
1360 LET U(J)=CODE N$
1370 NEXT J: FOR H=22949 TO 22964: POKE H,7: NEXT H: FOR J=1 TO 5: LET U$(J)=CHR$ U(J): POKE 47575+J,U(J): NEXT J: PRINT AT 17,4;"KEYS NOW IN USE.": FOR H=1 TO 40: NEXT H: CLS : RETURN
1380 OVER 0: PRINT AT 0,1;"  ARE YOU SURE? (OPTION 6)  "
1390 LET V$=INKEY$
1400 IF V$="Y" OR V$="N" THEN GO SUB 1140: OVER 1: RETURN
1410 GO TO 1390
1420 LET POS=PEEK (55086+75*LEV)
1430 LET AY=FN Z(POS)-1: LET AX=FN X(POS)+1
1440 IF AY<22 THEN PRINT AT AY,AX; OVER 1; INK 8; PAPER 0; BRIGHT 8;"++"
1450 IF AY>21 THEN PRINT #0;AT AY-22,AX; OVER 1; INK 8; PAPER 0; BRIGHT 8;"++"
1460 IF AX<26 THEN PRINT AT AY-2,AX+3; OVER 1; INK 8; PAPER 0; BRIGHT 8;"+";AT AY-1,AX+3;"+"
1470 LET J$=INKEY$
1480 LET OPOS=POS
1490 IF J$=U$(3) THEN LET POS=POS-6: IF POS<0 THEN LET POS=POS+36
1500 IF J$=U$(4) THEN LET POS=POS+6: IF POS>35 THEN LET POS=POS-36
1510 IF J$=U$(1) THEN LET POS=POS-1: IF (POS+1)/6=INT ((POS+1)/6) THEN LET POS=POS+6
1520 IF J$=U$(2) THEN LET POS=POS+1: IF POS/6=INT (POS/6) THEN LET POS=POS-6
1530 IF J$=U$(5) THEN GO SUB 1650: IF H=0 THEN IF M$<"6" THEN GO TO 1430
1540 IF J$=U$(5) THEN IF H=58 THEN GO TO 1430
1550 IF J$=U$(5) THEN IF H=0 THEN IF M$="6" THEN IF V$="Y" THEN RETURN
1560 IF J$=U$(5) THEN IF H=59 THEN RETURN
1570 IF J$>"1" AND J$<"7" THEN GO SUB 1610: LET M$=J$: GO SUB 800: GO TO 1430
1580 IF J$=CHR$ 7 THEN GO SUB 1100
1590 IF POS=OPOS THEN GO TO 1470
1600 GO SUB 1610: GO TO 1430
1610 IF AX<26 THEN PRINT AT AY-2,AX+3; OVER 1; INK 8; PAPER 0; BRIGHT 8;"+";AT AY-1,AX+3;"+"
1620 IF AY<22 THEN PRINT AT AY,AX; OVER 1; INK 8; PAPER 0; BRIGHT 8;"++"
1630 IF AY>21 THEN PRINT #0;AT AY-22,AX; OVER 1; INK 8; PAPER 0; BRIGHT 8;"++"
1640 RETURN
1650 RANDOMIZE USR 55021
1660 POKE 22528+X2+Y2*32,PEEK (22528+X2+Y2*32)+128
1670 LET X3=X2: LET Y3=Y2
1680 IF INKEY$=U$(3) THEN LET Y2=Y2-1: IF Y2=-1 THEN LET Y2=23
1690 IF INKEY$=U$(4) THEN LET Y2=Y2+1: IF Y2=24 THEN LET Y2=0
1700 IF INKEY$=U$(1) THEN LET X2=X2-1: IF X2=-1 THEN LET X2=21
1710 IF INKEY$=U$(2) THEN LET X2=X2+1: IF X2=22 THEN LET X2=0
1720 IF INKEY$=U$(5) THEN GO SUB 1750: RETURN
1730 IF X2<>X3 OR Y2<>Y3 THEN POKE 22528+X3+Y3*32,PEEK (22528+X3+Y3*32)-128: GO TO 1660
1740 GO TO 1670
1750 LET H=PEEK (47581+X2+Y2*22)
1760 POKE 22528+X3+Y3*32,PEEK (22528+X3+Y3*32)-128: RANDOMIZE USR 55021: IF H<41 THEN LET HJ=0
1770 IF H=50 AND POS>5 THEN LET R1=PEEK (LEV*75+55044+POS)-(INT (PEEK (LEV*75+55044+POS)/16))*16: IF R1>0 AND R1<>10 THEN RETURN
1780 IF H=50 THEN IF POS<6 THEN RETURN
1790 IF H>47 AND H<51 THEN IF POS=PEEK (LEV*75+55086) OR PEEK (55089+LEV*75+POS)>0 THEN RETURN
1800 IF H=0 THEN GO SUB 1610: GO SUB 750: RETURN
1810 IF H<53 AND H>40 THEN LET HJ=1
1820 IF H<57 AND H>52 THEN LET HJ=2
1830 IF H>56 THEN LET HJ=3
1840 IF HJ=1 AND POS<30 AND H<>50 THEN IF PEEK (LEV*75+55056+POS)-(INT (PEEK (LEV*75+55056+POS)/16))*16=10 THEN RETURN
1850 IF HJ=0 THEN LET DS=PEEK (LEV*75+55050+POS)-(INT (PEEK (LEV*75+55050+POS)/16))*16: IF (DS>7 AND DS<11) OR POS=F THEN RETURN
1860 IF H=57 THEN LET DS=PEEK (55050+LEV*75+POS): IF (DS>7 AND DS<11) OR PEEK (55089+LEV*75+POS)>0 THEN RETURN
1870 IF HJ=2 AND (POS+1)/6=INT ((POS+1)/6) THEN RETURN
1880 IF HJ=0 THEN LET BB=PEEK (LEV*75+55089+POS): LET G=POS: GO SUB 1070: POKE LEV*75+55089+POS,H
1890 IF HJ=1 OR HJ=2 THEN LET B1=INT (PEEK (LEV*75+55050+POS)/16): LET B2=PEEK (LEV*75+55050+POS)-B1*16
1900 IF HJ=1 OR HJ=2 THEN LET B1=INT (PEEK (LEV*75+55050+POS)/16): LET B2=PEEK (LEV*75+55050+POS)-B1*16
1910 IF HJ=1 THEN POKE LEV*75+55050+POS,B1*16+H-40
1920 IF HJ=2 THEN POKE LEV*75+55050+POS,16*H+B2-832
1930 IF HJ<3 THEN GO TO 2000
1940 IF H=57 THEN GO SUB 2050
1950 IF H=58 THEN GO SUB 1610: GO SUB 2070
1960 IF H=59 THEN RETURN
1970 IF H=60 THEN RUN
1980 IF H=61 THEN GO SUB 2180
1990 RETURN
2000 IF HJ=0 THEN LET BB=H: GO SUB 1070: RETURN
2010 IF HJ=1 THEN LET G=POS: GO SUB 980: LET B2=H-40: GO SUB 980: RETURN
2020 IF HJ=2 THEN LET G=POS: GO SUB 1010: LET B1=H-52: GO SUB 1010: RETURN
2030 DATA 96,0,0,2,0,0,0,0,100,0,0,2,104,0,1,6,106,0,0,2,110,-1,3,5,116,-1,0,4,122,-1,0,11,130,-1,0,11,138,-4,0,10,191,0,0,9,158,0,0,2
2040 DATA -2,1,-1,0,-3,4,-3,0,-3,3,-1,1,-2,1,-2,2,-2,0,-5,0,-2,0,-3,1,-1,1
2050 GO SUB 1030
2060 POKE LEV*75+55086,POS: GO SUB 1030: RETURN
2070 OVER 0: LET Z$=" 00"( TO 3-LEN STR$ PEEK (LEV*75+55087))+STR$ PEEK (LEV*75+55087)+" ": PRINT AT 0,26; PAPER 6; INK 0;Z$
2080 PRINT AT 0,1;"OBJECTS NEEDED TO FINISH:"
2090 LET BS=PEEK (55087+LEV*75)
2100 LET N$=INKEY$: IF N$<>U$(1) AND N$<>U$(2) AND N$<>U$(3) AND N$<>U$(4) AND N$<>U$(5) THEN GO TO 2100
2110 IF N$=U$(2) THEN LET BS=BS+1: IF BS=36 THEN LET BS=1
2120 IF N$=U$(1) THEN LET BS=BS-1: IF NOT BS THEN LET BS=35
2130 IF N$=U$(3) THEN LET BS=BS+5: IF BS>35 THEN LET BS=BS-35
2140 IF N$=U$(4) THEN LET BS=BS-5: IF BS<1 THEN LET BS=BS+35
2150 IF N$=U$(5) THEN POKE 55087+LEV*75,BS: GO SUB 1140: RETURN
2160 LET Z$=" 00"( TO 3-LEN STR$ BS)+STR$ BS+" ": PRINT AT 0,26; PAPER 6; INK 0;Z$
2170 GO TO 2100
2180 LET J=22528+FN Z(PEEK (LEV*75+55088))*32+FN X(PEEK (LEV*75+55088)): FOR H=J TO J+3: POKE H,PEEK H-128: NEXT H: POKE 55088+LEV*75,POS
2190 LET J=22528+FN Z(POS)*32+FN X(POS): FOR H=J TO J+3: POKE H,PEEK H+128: NEXT H
2200 RETURN
2210 CLEAR : OVER 0: INVERSE 0: FLASH 0
2220 SAVE "BPY EDITOR" LINE 30
2230 SAVE "CODE"CODE 47419,18117
2240 CLS : PRINT AT 7,7; PAPER 4; BRIGHT 1; INK 0;"REWIND TAPE!": INK 0: VERIFY "BPY EDITOR"
2250 VERIFY "CODE"CODE
2260 INK 6: PRINT ' INK 5'"        ALL OKAY!": PAUSE 0: RUN
2270 PRINT AT 0,0;" FILENAME: ";: INK 3
2280 LET ACC=22539: LET ATW=249
2290 POKE ACC,ATW: LET ATW=249
2300 PAUSE 0
2310 LET N$=INKEY$: IF NOT ((N$>="A" AND N$<="Z") OR N$=CHR$ 12 OR N$=CHR$ 13 OR N$=CHR$ 32) THEN GO TO 2310
2320 IF (CODE N$>64 AND CODE N$<91) OR N$=" " THEN IF ACC<22549 THEN PRINT AT 0,ACC-22528;N$: LET ACC=ACC+1: GO TO 2290
2330 IF N$=CHR$ 12 THEN IF ACC>22539 THEN LET ACC=ACC-1: PRINT AT 0,ACC-22528;"  ": GO TO 2290
2340 IF N$=CHR$ 13 THEN LET F$="": FOR T=ACC-22529 TO 11 STEP -1: LET F$=SCREEN$ (0,T)+F$: NEXT T: INK 6: POKE ACC,70: RETURN
2350 IF N$=CHR$ 12 OR N$=CHR$ 32 OR (N$>="A" AND N$<="Z") THEN LET ATW=250
2360 GO TO 2290
2370 IF INKEY$=U$(5) THEN GO TO 2370
2380 LET X$=" 00"( TO 3-LEN STR$ L)+STR$ L+" ": LET KH=11: LET KJ=4: GO SUB 330: PRINT AT 11,20; INK 5;"= -1";AT 13,20;"= +1";AT 15,20;"= +5";AT 17,20;"= -5";AT 20,20;"= SELECT";AT 5,0; INK 4;"UP TO AND INCLUDING LEVEL: "; PAPER 6; INK 0;X$: OVER 0
2390 PLOT 0,0: DRAW 255,0: DRAW 0,95: DRAW -255,0: DRAW 0,-95: PLOT 2,2: DRAW 251,0: DRAW 0,91: DRAW -251,0: DRAW 0,-91
2400 LET N$=INKEY$: IF N$<>U$(3) AND N$<>U$(4) AND N$<>U$(5) AND N$<>U$(1) AND N$<>U$(2) THEN GO TO 2400
2410 IF N$=U$(4) THEN LET L=L-5: IF L<0 THEN LET L=L+100
2420 IF N$=U$(3) THEN LET L=L+5: IF L>99 THEN LET L=L-100
2430 IF N$=U$(2) THEN LET L=L+1: IF L=100 THEN LET L=0
2440 IF N$=U$(1) THEN LET L=L-1: IF NOT L+1 THEN LET L=99
2450 IF N$=U$(5) THEN RETURN
2460 LET Z$=" 00"( TO 3-LEN STR$ L)+STR$ L+" ": PRINT AT 5,27; PAPER 6; INK 0;Z$
2470 GO TO 2400
2480 DATA 197,7,200,6,202,6,204,1,208,1,212,1,216,7,219,1,223,1,227,1
2490 DATA 231,1,235,6,237,1,241,6,243,1,247,6,249,1,253,6,255,1,259,1
2500 DATA 263,1,267,1,271,1,275,1,279,1,283,1,287,1,291,1,295,1,299,1
2510 DATA 303,8,306,1,310,8,313,1,317,1,321,1,325,1,329,1,333,1,337,1
